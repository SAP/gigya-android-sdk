apply plugin: 'com.android.library'

def androidVersion = file('version.txt').text

ext {
    outDir = "${projectDir}/dist"
    androidJarName = "gigya-android-sdk-${androidVersion}"
    androidDocName = "gigya-android-sdk-${androidVersion}-javadoc"
    androidSrcName = "gigya-android-sdk-${androidVersion}-source"
    sharedManifest = manifest {
        attributes(
                'Implementation-Vendor': 'SAP Gigya INC.',
                'Implementation-Title': 'Gigya Android SDK',
                'Implementation-Version': androidVersion
        )
    }
}

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        //noinspection MinSdkTooLow
        minSdkVersion build_versions.min_sdk_core
        targetSdkVersion build_versions.target_sdk

        archivesBaseName = "gigya-android-sdk-${androidVersion}"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {

        debug {
            //testCoverageEnabled true
            debuggable true
        }

        release {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    packagingOptions {
        exclude 'META-INF/proguard/androidx-annotations.pro'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

}

dependencies {

    // Test implementations.
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.json:json:20141113'
    testImplementation 'androidx.test:core:1.2.0'
    testImplementation 'org.mockito:mockito-core:2.8.9'
    testImplementation 'org.powermock:powermock-module-junit4:1.7.4'
    testImplementation 'org.powermock:powermock-api-mockito2:1.7.4'
    testImplementation 'org.apache.httpcomponents:httpcore:4.4.11'
    testImplementation 'org.apache.httpcomponents:httpclient:4.5.9'
    testImplementation 'com.android.volley:volley:1.1.1'
    testImplementation project(path: ':sdk-tfa')
    testImplementation project(path: ':sdk-biometric')

    // Implementations.
    //noinspection GradleCompatible,GradleDependency
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.google.code.gson:gson:2.8.5'

    // Compile only libraries. Not included in packaged JAR file.
    compileOnly files('libs/line-sdk-4.0.10.aar')
    compileOnly 'com.facebook.android:facebook-android-sdk:5.5.0'
    compileOnly 'com.google.android.gms:play-services-auth:16.0.1'
    compileOnly 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:5.5.3'
    compileOnly 'com.android.volley:volley:1.1.1'

    compileOnly 'com.google.firebase:firebase-messaging:18.0.0'
}



// Task for building release Jar/sources/JavaDocs.
android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (name == 'release') {

        task createOutputDirs() {
            file(outDir).mkdir()
        }
        createOutputDirs.outputs.dir files(
                file(outDir),
                buildDir
        )
        task removeOutputDirs() {
            delete outDir, buildDir
        }

        task createAndroidJar(type: Jar, dependsOn: variant.javaCompileProvider) {
            baseName androidJarName
            from variant.javaCompileProvider.get().destinationDir
           // from { configurations.extraLibs.collect { it.isDirectory() ? it : zipTree(it) } }
            destinationDir = file(outDir)
            include(
                    'com/gigya/android/sdk/**'
            )
            manifest {
                from sharedManifest
            }
        }

        task createAndroidJavadoc(type: Javadoc) {
            source = project.android.sourceSets.main.java.srcDirs
            options.linkSource true
            classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
            destinationDir = new File("${outDir}/${androidDocName}")
            include(
                    'com/gigya/android/sdk/*.java',
                    'com/gigya/android/sdk/account/*.java',
                    'com/gigya/android/sdk/account/models/*.java',
                    'com/gigya/android/sdk/api/*.java',
                    'com/gigya/android/sdk/api/models/*.java',
                    'com/gigya/android/sdk/containers/*.java',
                    'com/gigya/android/sdk/encryption/*.java',
                    'com/gigya/android/sdk/interruption/*.java',
                    'com/gigya/android/sdk/interruption/link/*.java',
                    'com/gigya/android/sdk/interruption/link/models/*.java',
                    'com/gigya/android/sdk/interruption/tfa/*.java',
                    'com/gigya/android/sdk/interruption/tfa/modeks/*.java',
                    'com/gigya/android/sdk/network/*.java',
                    'com/gigya/android/sdk/network/adapter/*.java',
                    'com/gigya/android/sdk/persistence/*.java',
                    'com/gigya/android/sdk/providers/*.java',
                    'com/gigya/android/sdk/providers/provider/*.java',
                    'com/gigya/android/sdk/session/*.java',
                    'com/gigya/android/sdk/ui/*.java',
                    'com/gigya/android/sdk/ui/plugin/*.java',
                    'com/gigya/android/sdk/ui/provider/*.java',
                    'com/gigya/android/sdk/utils/*.java'
            )
            failOnError = false
        }

        task createAndroidSrc(type: Copy, dependsOn: variant.javaCompileProvider) {
            from 'src'
            def sourceDir = new File("${outDir}/${androidSrcName}")
            sourceDir.mkdir()
            into sourceDir
        }

        task createPropertiesFile(dependsOn: [createAndroidJar, createAndroidJavadoc, createAndroidSrc]) {
            File propertiesFile = new File("${outDir}/${androidJarName}.jar.properties")
            doLast {
                propertiesFile.text = "src=${androidSrcName}\n"
                propertiesFile.text += "doc=${androidDocName}"
            }
        }

        task release(dependsOn: createPropertiesFile) {
            println 'Release: Done'
        }
    }
}

