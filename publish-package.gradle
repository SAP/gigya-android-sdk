apply plugin: 'maven-publish'
apply plugin: 'signing'
apply plugin: 'org.jetbrains.dokka'

def signingProperties = new Properties()
File signingPropertiesFile = project.rootProject.file('publish-signing.properties')
if (signingPropertiesFile.exists()) {
    signingProperties.load(new FileInputStream(signingPropertiesFile))
}

group = project.group
version = project.ext.version

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    from components.java
                }

                // Note: Both sources jar and javadoc jar are automatically included via 'from components.release'
                // when withSourcesJar() and withJavadocJar() are configured in the android.publishing block

                groupId = project.group
                artifactId = project.ext.artifactId
                version = project.ext.version

                pom {
                    name = project.ext.name
                    description = project.ext.description
                    url = project.ext.url
                    artifactId = project.ext.artifactId
                    packaging = 'aar'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            name = 'Tal Mirmelshiten'
                            email = 'tal.mirmelshtein@sap.com'
                            organization = 'SAP'
                        }
                    }

                    scm {
                        url = 'https://github.com/SAP/gigya-android-sdk'
                        connection = 'scm:git:git://git@github.com:SAP/gigya-android-sdk.git'
                        developerConnection = 'scm:git:ssh://git@github.com:SAP/gigya-android-sdk.git'
                    }
                }
            }
        }
        repositories {
            maven {
                name = "sonatype"
                url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username = signingProperties['ossrhUsername']
                    password = signingProperties['ossrhPassword']
                }
            }
        }
    }
}

// Fix for Jitpack build: Ensure metadata generation waits for jar creation
// This resolves the implicit dependency issue with generateMetadataFileForReleasePublication
afterEvaluate {
    tasks.withType(GenerateModuleMetadata).configureEach {
        // The releaseSourcesJar and releaseJavadocJar tasks are created by withSourcesJar()
        // and withJavadocJar() in the android.publishing block
        def releaseSourcesJar = tasks.findByName('releaseSourcesJar')
        if (releaseSourcesJar != null) {
            dependsOn(releaseSourcesJar)
        }
        
        def releaseJavadocJar = tasks.findByName('releaseJavadocJar')
        if (releaseJavadocJar != null) {
            dependsOn(releaseJavadocJar)
        }
    }
}
